[[mqtt]]
== Neo4j MqTT Cypher Client

 
[abstract]
--
MqTT publish subscribe from Neo4j to MqTT broker
--

[NOTE]
====
 based on org.eclipse.paho.client.mqttv3 library 
 tested with Eclipse Mosquitto 1.5.8 MqTT broker
 only tcp currently supported
====

// end::mqtt[]

=== Managing MqTT brokers

MqTT broker - connect, list, disconnect
[source,cypher]
----
// --- Register MqTT Broker ---
// all values under connect options are optional - default values are shown
RETURN apoc.mqtt.connectBroker(
 'mqttBrokerId', 
 {
    serverURI:'tcp://localhost:1883',
    clientId:'mqttBrokerId+-+random',
    qos:0, 
    automaticReconnect:true, 
    cleanSession:false, 
    connectionTimeout:1  
 }
)

// --- List MqTT Brokers ---
// basic statistics are shown
RETURN apoc.mqtt.listBrokers()

// --- List MqTT Brokers vNode returned ---
// basic statistics are shown
RETURN apoc.mqtt.listBrokersAsVnode()

// --- Remove MqTT Broker ---
RETURN apoc.mqtt.disconnectBroker('mqttBrokerLocalhost')
----

Neo4j MqTT broker registration response messages
[source,cypher]
----
// --- succesful mqtt registration ---
{
  "mqttBrokerId": "mqttLocalhostBroker",
  "serverURI": "tcp://localhost:1883",
  "statusMessage": "Connect MqTT OK",
  "status": "ok"
}

// --- failed mqtt registration ---
{
  "mqttBrokerId": "mqttLocalhostBroker",
  "serverURI": "tcp://localhost:1883",
  "statusMessage": "Broker with this name exists!",
  "status": "error"
}

// --- failed mqtt registration ---
{
  "mqttBrokerId": "mqttLocalhostBrokerError",
  "serverURI": "tcp://localhostNotExists:1883",
  "statusMessage": "Failed to Connect MqTT Broker: MqttException",
  "status": "error"
}
----

=== MqTT publish
MqTT publish from Neo4j
[source,cypher]
----
// --- Publish MqTT message ---
RETURN apoc.mqtt.publish(
    'mqttBrokerId', 
    'mqtt/topic/path', 
    {mqttMessageValue:"message as json or value"}, 
    {options:"publish options - currently only debug", debug:false}
)
----

Neo4j MqTT publish response messages
[source,bash]
----
// --- succesful mqtt publish response ---
{
  "options": {
    "debug": true
  },
  "mqttBrokerId": "mqttLocalhostBroker",
  "topic": "mqtt/topic/json",
  "message": {
    "string": "string",
    "int": 123,
    "boolean": true
  },
  "statusMessage": "MqTT Publish OK",
  "status": "ok"
}
// --- Neo4j log ---
2019-11-17 08:21:05.147+0000 INFO  apoc.mqtt.publish:
 mqttBrokerId: mqttLocalhostBroker
 toppic: mqtt/topic/json
 message: {boolean=true, string=string, int=123}
 publish status: OK


// --- failed mqtt publish ---
{
  "options": {
    "debug": true
  },
  "mqttBrokerId": "mqttLocalhostBrokerNotExists",
  "topic": "mqtt/topic/json",
  "message": {
    "string": "string",
    "int": 123,
    "boolean": true
  },
  "statusMessage": "Failed to find MqTT Broker - Check Connection",
  "status": "error"
}
// --- Neo4j log ---
2019-11-17 09:06:43.858+0000 INFO apoc.mqtt.publish:
 mqttBrokerId: mqttLocalhostBrokerNotExists
 toppic: mqtt/topic/json
 message: {boolean=true, string=string, int=123}
 publish status: ERROR Failed to find MqTT Broker - Check Connection
----

=== MqTT subscribe
Subscribe, list, unsubscribe MqTT topics and run Neo4j cypher
[source,cypher]
----
// --- Subscribe to MqTT Topic Procedure ---
CALL apoc.mqtt.subscribe(
    'mqttBrokerId', 
    'mqtt/topic/path',
    'MERGE (n:CypherMqttCreated) 
        ON CREATE SET n.count=1, n.mqttMessageValue=$mqttMessageValue 
        ON MATCH SET n.count = n.count +1, n.mqttMessageValue=$mqttMessageValue 
     RETURN n', 
     {options:"subscribe options - currently only debug", debug:false}
 )

// --- List Subscriptions ---
// basic subscription statistics are shown
RETURN apoc.mqtt.listSubscriptions('optionalBrokerIdDefaultToAll')

// --- List Subscriptions vNode Returned ---
// basic subscription statistics are shown
RETURN apoc.mqtt.listSubscriptionsAsVnode('optionalBrokerIdDefaultToAll')

// --- Remove MqTT subscription ---
RETURN apoc.mqtt.unSubscribe('mqttBrokerId', '/mqtt/topic/path' )
----


=== Examples
==== Publish MqTT messages from Neo4j
[NOTE]
====
 MqTT broker should be operational!
====


Start Mosquitto MqTT subscription client
[source,bash]
----
mosquitto_sub  --verbose --host localhost --port 1883 --topic mqtt/topic/# --id msqSub

----

Register localhost MqTT broker via Neo4j console
[source,cypher]
----
RETURN apoc.mqtt.connectBroker(
 'mqttLocalhostBroker', 
 {
    serverURI:'tcp://localhost:1883',
    clientId:'neo4jClient01'
  }
)
----

Publish MqTT messages via Neo4j console
[source,cypher]
----
// --- json ---
RETURN apoc.mqtt.publish(
    'mqttLocalhostBroker', 
    'mqtt/topic/json', 
    {
        string:"string",
        int:123,
        boolean:true
    }
)

// --- string value ---
RETURN apoc.mqtt.publish(
    'mqttLocalhostBroker', 
    'mqtt/topic/valueString', 
    "stringValue"
)

// --- integer value ---
RETURN apoc.mqtt.publish(
    'mqttLocalhostBroker', 
    'mqtt/topic/valueInteger', 
    123
)

// --- number value ---
RETURN apoc.mqtt.publish(
    'mqttLocalhostBroker', 
    'mqtt/topic/valueNumber', 
    123.456
)

// --- boolean value ---
RETURN apoc.mqtt.publish(
    'mqttLocalhostBroker', 
    'mqtt/topic/valueBoolean', 
    true
)

// --- Neo4j node ---
CREATE (neo4jNode:TestMqttNode:TestNode {string:"string", int:123, boolean:true})
WITH neo4jNode
RETURN apoc.mqtt.publish(
    'mqttLocalhostBroker', 
    'mqtt/topic/neo4jNode', 
    neo4jNode
)

// --- Neo4j relation ---
CREATE (s)-[r:TEST_MQTT_RELATION {string:"string", int:123, boolean:true}]->(e) 
WITH r AS neo4jRelation
RETURN apoc.mqtt.publish(
    'mqttLocalhostBroker', 
    'mqtt/topic/neo4jRelation', 
    neo4jRelation
)

----

Check MqTT messages received by Mosquitto MqTT client
[source,bash]
----
mosquitto_sub  --verbose --host localhost --port 1883 --topic mqtt/topic/# --id msqSub
mqtt/topic/json {"boolean":true,"string":"string","int":123}
mqtt/topic/valueString stringValue
mqtt/topic/valueInteger 123
mqtt/topic/valueNumber 123.456
mqtt/topic/valueBoolean true
mqtt/topic/neo4jNode {"id":80,"properties":{"boolean":true,"string":"string","int":123},"labels":"TestMqttNode:TestNode"}
mqtt/topic/neo4jRelation {"startNodeId":60,"id":8,"type":"TEST_MQTT_RELATION","endNodeId":61,"properties":{"boolean":true,"string":"string","int":123}}

----



==== Send MqTT message on node add, delete and status change

Start Mosquitto MqTT subscription client
[source,bash]
----
mosquitto_sub  --verbose --host localhost --port 1883 --topic graph/# --id msqSubStatus
----

Setup Neo4j APOC triggers and MqTT publish
[source,cypher]
----
// --- connect to broker ---
RETURN apoc.mqtt.connectBroker(
 'mqttLocalhostBroker', 
 {
    serverURI:'tcp://localhost:1883',
    clientId:'neo4jClient01'
  }
)

// --- optionally - remove all triggers ---
CALL apoc.trigger.removeAll() 

// --- add apoc trigger to send mqtt message on node add ---
CALL apoc.trigger.add(
    'sendMqttMessageOnNodeAdd',
    'UNWIND {createdNodes} as node
    WITH node
    RETURN apoc.mqtt.publish(
        "mqttLocalhostBroker", 
        "graph/node/add", 
        node
    )',    
    {phase:'after'}
)

// --- add apoc trigger to send mqtt message on node delete ---
CALL apoc.trigger.add(
    'sendMqttMessageOnNodeDelete',
    'UNWIND {deletedNodes} as node
    WITH id(node) as id
    RETURN apoc.mqtt.publish(
        "mqttLocalhostBroker", 
        "graph/node/delete", 
        {id:id}
    )',    
    {phase:'after'}
)

// --- add apoc trigger to send mqtt message on node status change ---
CALL apoc.trigger.add(
    'sendMqttMessageOnNodeStatusChange',
    'UNWIND apoc.trigger.propertiesByKey({assignedNodeProperties},"status") as prop
    WITH prop.node as n
    RETURN apoc.mqtt.publish(
        "mqttLocalhostBroker", 
        "graph/node/status/update", 
        n
    )',    
    {phase:'after'}
)

// --- list triggers ---
CALL apoc.trigger.list()
----

Run test scenario
[source,cypher]
----

CREATE (n:TestNode {name:"test", status:"initialized"})

MATCH (n:TestNode)
 SET n.status="up"

MATCH (n:TestNode)
 SET n.status="down"

MATCH (n:TestNode) DETACH DELETE n
----



Check received messages
[source,bash]
----
mosquitto_sub  --verbose --host localhost --port 1883 --topic graph/# --id msqSubStatus
graph/node/status/update {"id":239,"properties":{"name":"test","status":"initialized"},"labels":"TestNode"}
graph/node/add {"id":239,"properties":{"name":"test","status":"initialized"},"labels":"TestNode"}
graph/node/status/update {"id":239,"properties":{"name":"test","status":"up"},"labels":"TestNode"}
graph/node/status/update {"id":239,"properties":{"name":"test","status":"down"},"labels":"TestNode"}
graph/node/delete {"id":239}
----


==== Subscribe to MqTT topic process messages and run CYPHER from Neo4j

Register localhost MqTT broker via Neo4j console
[source,cypher]
----
RETURN apoc.mqtt.connectBroker(
 'mqttLocalhostBroker', 
 {
    serverURI:'tcp://localhost:1883',
    clientId:'neo4jClient01'
  }
)
----

Subscribe to MqTT messages via Neo4j console
[source,cypher]
----
// --- json mqtt message subscription ---
CALL apoc.mqtt.subscribe(
    'mqttLocalhostBroker', 
    'mqtt/topic/json',
    'MERGE (n:MqttCreatedNodeJson) 
        ON CREATE SET 
            n.count=1, 
            n.string=$string,
            n.int=$int,
            n.boolean=$boolean
        ON MATCH SET 
            n.count = n.count +1, 
            n.string=$string,
            n.int=$int,
            n.boolean=$boolean 
     RETURN n'
 )
 
 // --- "value" mqtt message subscription ---
 CALL apoc.mqtt.subscribe(
    'mqttLocalhostBroker', 
    'mqtt/topic/value/#',
    'CREATE (n:MqttCreatedNodeValue) SET  n.value=$value   RETURN n'
 )

----
 
 
Send MqTT messages
[source,bash]
----
# send json message
mosquitto_pub  --id msqPub --host localhost --port 1883 --topic mqtt/topic/json --message "{\"string\":\"string\",\"int\":123,\"boolean\":true}" 
# send "value" message
mosquitto_pub  --id msqPub --host localhost --port 1883 --topic mqtt/topic/value/string --message "string"
mosquitto_pub  --id msqPub --host localhost --port 1883 --topic mqtt/topic/value/int --message 123
mosquitto_pub  --id msqPub --host localhost --port 1883 --topic mqtt/topic/value/float --message 123.456
----

 
 
Check Subscriptions
[source,cypher]
----
[
{
  "mqttBrokerId": "mqttLocalhostBroker",
  "topic": "mqtt/topic/json",
  "type": "MqttSubscription",
  "subscribeOptions": {
    "lastMessageReceived": "{"string":"string","int":123,"boolean":true}",
    "lastMessageProcessedResults": "+----------------------------------------------------------+
| n                                                        |
+----------------------------------------------------------+
| Node[82]{boolean:true,string:"string",count:2,int:123.0} |
+----------------------------------------------------------+
1 row
Properties set: 4
",
    "messageReceivedOk": 1,
    "debug": false,
    "messageReceivedError": 0,
    "query": "MERGE (n:MqttCreatedNodeJson)
        ON CREATE SET
            n.count=1,
            n.string=$string,
            n.int=$int,
            n.boolean=$boolean
        ON MATCH SET
            n.count = n.count +1,
            n.string=$string,
            n.int=$int,
            n.boolean=$boolean
     RETURN n",
    "mqttBrokerId": "mqttLocalhostBroker"
  }
}
,
{
  "mqttBrokerId": "mqttLocalhostBroker",
  "topic": "mqtt/topic/value/#",
  "type": "MqttSubscription",
  "subscribeOptions": {
    "lastMessageReceived": "string",
    "lastMessageProcessedResults": "+---------------------------+
| n                         |
+---------------------------+
| Node[267]{value:"string"} |
+---------------------------+
1 row
Nodes created: 1
Properties set: 1
Labels added: 1
",
    "messageReceivedOk": 1,
    "debug": false,
    "messageReceivedError": 0,
    "query": "CREATE (n:MqttCreatedNodeValue) SET  n.value=$value   RETURN n",
    "mqttBrokerId": "mqttLocalhostBroker"
  }
}
]
----

Check Nodes
----
// --- get created nodes
MATCH (n) 
WHERE n:MqttCreatedNodeJson OR n:MqttCreatedNodeValue
RETURN {id:id(n), labels:labels(n), properties:properties(n)}

// --- response
{id:id(n), labels:labels(n), properties:properties(n)}
{
  "id": 82,
  "properties": {
    "boolean": true,
    "string": "string",
    "count": 1,
    "int": 123.0
  },
  "labels": [
    "MqttCreatedNodeJson"
  ]
}
{
  "id": 102,
  "properties": {
    "value": "string"
  },
  "labels": [
    "MqttCreatedNodeValue"
  ]
}
{
  "id": 121,
  "properties": {
    "value": "123"
  },
  "labels": [
    "MqttCreatedNodeValue"
  ]
}
{
  "id": 122,
  "properties": {
    "value": "123.456"
  },
  "labels": [
    "MqttCreatedNodeValue"
  ]
}
{
  "id": 123,
  "properties": {
    "value": "string"
  },
  "labels": [
    "MqttCreatedNodeValue"
  ]
}
{
  "id": 124,
  "properties": {
    "value": "123"
  },
  "labels": [
    "MqttCreatedNodeValue"
  ]
}
{
  "id": 125,
  "properties": {
    "value": "123.456"
  },
  "labels": [
    "MqttCreatedNodeValue"
  ]
}
----
